apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version = '3.2-SNAPSHOT'
group = 'org.languagetool'

String langCode="uk"
String packageDir="org/languagetool/resource/" + langCode
String resourceDir="src/main/resources/" + packageDir
String outputDir="build/resources/main/" + packageDir

String inputDir="../../out/prev"
String dstEncoding="cp1251"
String srcEncoding="utf-8"

String tmpDir="build/tmp"


repositories {
    mavenLocal()
    mavenCentral()
}

configurations{
  provided {
        description = 'Configuration for generating the dictionaries'
  }
}

dependencies {
    provided 'org.languagetool:languagetool-tools:3.2-SNAPSHOT'
}



task prepareDict(type: Exec) {
    def srcDict="${inputDir}/dict_rules_lt.txt"
    def outFile="${tmpDir}/all.tagged.tmp"

    inputs.file srcDict
    outputs.file outFile

    workingDir "$projectDir"

    doFirst {
        new File(outputDir).mkdirs()
        new File(tmpDir).mkdirs()
    }

    def cmd = "cat ${srcDict} | iconv -f ${srcEncoding} -t ${dstEncoding} | tr ' ' '\\t' | LC_ALL=POSIX sort -u > ${outFile}"
    cmd += " ; cat ${srcDict} | awk '{ print \$3 }' | sort | uniq > ${outputDir}/ukrainian_tags.txt"

    commandLine "bash", "-c", "${cmd}"
}

task createPOSDict(type: JavaExec, dependsOn: prepareDict) {
    def outputDict="${outputDir}/ukrainian.dict"

    inputs.file tasks.prepareDict.outputs.files
    outputs.file outputDict

    workingDir "$projectDir"

    classpath = files(configurations.provided.files)
    main = 'org.languagetool.tools.POSDictionaryBuilder'

    args "-i", "${tmpDir}/all.tagged.tmp"
    args "-info", "${resourceDir}/ukrainian.info"
    args "-o", "${outputDict}"
}

task createSynthDict(type: JavaExec, dependsOn: prepareDict) {
    def outputDict="${outputDir}/ukrainian_synth.dict"

    inputs.file tasks.prepareDict.outputs.files
    outputs.file outputDict

    workingDir "$projectDir"

    classpath = files(configurations.provided.files)
    main = 'org.languagetool.tools.SynthDictionaryBuilder'

    args "-i", "${tmpDir}/all.tagged.tmp"
    args "-info", "src/main/resources/" + packageDir + "/ukrainian_synth.info"
    args "-o", "${outputDict}"
}


task createSpellDict(type: Exec) {
    def srcDict="${inputDir}/words.txt"
    def spellOutDir="${outputDir}/hunspell"
    def outFile="${spellOutDir}/uk_UA.dict"
    def tmpFile="${tmpDir}/all.words.tmp"
    def freqFile="src/main/data/uk_wordlist.xml"

    inputs.file srcDict
    outputs.file outFile

    workingDir "$projectDir"

    doFirst {
        new File(spellOutDir).mkdirs()
    }

    def classpath = configurations.provided.files.join(":")

    def cmd = "cat ${srcDict} | grep -v '\\.\$' | iconv -f ${srcEncoding} -t ${dstEncoding} | LC_ALL=POSIX sort -u > ${tmpFile} &&"
    cmd += " java -cp ${classpath} org.languagetool.tools.SpellDictionaryBuilder"
    cmd += " -i ${tmpFile} -info src/main/resources/" + packageDir + "/hunspell/uk_UA.info -freq ${freqFile} -o ${outFile}"

    commandLine 'sh', '-c', "${cmd}"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Ukrainian dictionary for LanguageTool',
                   'Implementation-Version': version
    }
}

processResources.dependsOn(createPOSDict, createSynthDict, createSpellDict)

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
